plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
}

apply from: '../dependencies.gradle'

android {

    compileSdkVersion 31
    buildToolsVersion '30.0.0'

    defaultConfig {
        applicationId "com.app.meatz"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 3
        versionName "1.0"
        resConfigs "ar", "en" // Remove unused alternative resources
        vectorDrawables.useSupportLibrary = true
    }
    bundle {
        language {
            enableSplit = false
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled true // Shrink code by removing unused methods
            shrinkResources true // Remove all the resources that are not used in the project
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable false
            minifyEnabled true // Shrink code by removing unused methods
            shrinkResources true // Remove all the resources that are not used in the project
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures { viewBinding true }

    kotlinOptions {
        jvmTarget = "1.8"
        javaParameters = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude("META-INF/*.kotlin_module")
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += [
                // Don’t generate not-null assertions for arguments of platform types
                '-Xno-call-assertions',
                // Don’t generate not-null assertion for extension receiver arguments of platform types
                '-Xno-receiver-assertions',
                // Don’t generate not-null assertions on parameters of methods accessible from Java
                '-Xno-param-assertions'
        ]
    }
}

dependencies {
    implementation platform('com.google.firebase:firebase-bom:26.7.0')
    implementation appDependency.values()
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt annotationDependency.values()
}

apply plugin: 'com.google.gms.google-services'